### FILE="Main.annotation"
## Copyright:	Public domain.
## Filename:	DOWN-TELEMETRY_PROGRAM.agc
## Purpose:	A section of Sunrise 45.
##		It is part of the reconstructed source code for the penultimate
##		release of the Block I Command Module system test software. No
##		original listings of this program are available; instead, this
##		file was created via disassembly of dumps of Sunrise core rope
##		memory modules and comparison with the later Block I program
##		Solarium 55.
## Assembler:	yaYUL --block1
## Contact:	Ron Burkey <info@sandroid.org>.
## Website:	www.ibiblio.org/apollo/index.html
## Mod history:	2022-12-09 MAS	Initial reconstructed source.

## Label names in this section were taken from the errata for AGC Information Series Issue 15.

# TELEMETRY PROCESSOR
# --------- ---------
#
#	THE FOLLOWING TELEMETRY PROGRAM IS DESIGNED TO TRANSMIT TELEMETRY DATA VIA OUT4 WHEN AN ENDPULSE 
# FROM THE NORTH AMERICAN TELEMETRY PROGRAMMER TRIGGERS INTERRUPT 6, WHICH INITIATES THIS ROUTINE. IT OPERATES
# IN CONJUNCTION WITH (BUT ASYNCHRONOUSLY FROM) THE T4RUPT PROGRAM, WHICH IS INITATED EVERY 60 MS, VIA INTERRUPT
# 3. 


		BANK	1
DOWNRUPT	CCS	TELCOUNT	# PNZ IS NORMAL SETTING - +0 INDICATES
		TC	DOWNTMOK	# TM FAILURE SINCE ENDPULSES ARE OCCURING
		TC	TMFAIL		# TOO FREQUENTLY. THE COUNTER IS SET TO +7
		
		CS	BIT10		# BLOCK TM ENDPULSES UNTIL  ERROR RESET
		MASK	OUT1		# COMMAND IS GIVEN.
		AD	BIT10
		TS	OUT1
		
		TC	NBRESUME	# BY DSRUPT EVERY 120 MS.
		
DOWNTMOK	TS	TELCOUNT	# NORMAL MODE - STORE DECREMENTED COUNT.

		CCS	DISPBUF
		TC	+2
		TC	CHARTEST

		CAF	ZERO
		XCH	DISPBUF

DATADWNF	TS	OUT4
		CS	BIT9
		MASK	OUT1
		TS	OUT1
		TC	NBRESUME	# NO BANK SWITCH REQUIRED.

CHARTEST	CCS	TMKEYBUF
		TC	+2
		TC	DATA/ID

		CAF	ZERO
		XCH	TMKEYBUF
		TC	DATADWNF

DATA/ID		CCS	IDPLACER
		TC	+2
		TC	IDNEXT

		TS	IDPLACER
		CCS	TMINDEX
		TC	+2
		CAF	DNLST1SZ
		TS	TMINDEX

		INDEX	A
		INDEX	DNLST1
		CS	0
		COM
		TS	OUT4
		CS	BIT9
		MASK	OUT1
		AD	BIT9
		TS	OUT1
		TC	NBRESUME

IDNEXT		AD	TMINDEX
		TS	OUT4
		CAF	FOUR
		TS	IDPLACER
		TC	DATADWNF +1

#	SUBROUTINE COMMON TO UPLINK AND DOWNLINK TO TURN ON THE TM FAIL LIGHT.

TMFAIL		CS	BIT4
		MASK	OUT1
		AD	BIT4
		TS	OUT1
		TC	Q

DNLST1SZ	DEC	31

DNLST1		ADRES	TIME1
		ADRES	TIME2
		ADRES	IN0
		ADRES	IN2
		ADRES	IN3
		ADRES	OUT1
		ADRES	RRECT
		ADRES	RRECT +1
		ADRES	RRECT +2
		ADRES	RRECT +3
		ADRES	RRECT +4
		ADRES	RRECT +5
		ADRES	TDELTAV
		ADRES	TDELTAV +1
		ADRES	TDELTAV +2
		ADRES	TDELTAV +3
		ADRES	TDELTAV +4
		ADRES	TDELTAV +5
		ADRES	VRECT
		ADRES	VRECT +1
		ADRES	VRECT +2
		ADRES	VRECT +3
		ADRES	VRECT +4
		ADRES	VRECT +5
		ADRES	TNUV
		ADRES	TNUV +1
		ADRES	TNUV +2
		ADRES	TNUV +3
		ADRES	TNUV +4
		ADRES	TNUV +5
		ADRES	GYROD +5
		ADRES	GYROD +3
