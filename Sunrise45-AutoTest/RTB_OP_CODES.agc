### FILE="Main.annotation"
## Copyright:	Public domain.
## Filename:	RTB_OP_CODES.agc
## Purpose:	A section of Sunrise 45.
##		It is part of the reconstructed source code for the penultimate
##		release of the Block I Command Module system test software. No
##		original listings of this program are available; instead, this
##		file was created via disassembly of dumps of Sunrise core rope
##		memory modules and comparison with the later Block I program
##		Solarium 55.
## Assembler:	yaYUL --block1
## Contact:	Ron Burkey <info@sandroid.org>.
## Website:	www.ibiblio.org/apollo/index.html
## Mod history:	2022-12-09 MAS	Initial reconstructed source.


		BANK	23
# ROUTINE TO LOAD TIME OF DAY INTO MPAC

LOADTIME	TC	READTIME
		CS	RUPTSTOR
		TS	MPAC
		CS	RUPTSTOR +1
		TS	MPAC +1
		RELINT
		CAF	ZERO
		TS	NEWEQIND
		TS	MPAC +2
		TC	DPEXIT


# ROUTINE TO RESET THE PUSHDOWN POUNTER

FRESHPD		CS	FIXLOC
		COM
		TS	PUSHLOC
		TC	RE-ENTER


# ROUTINE TO ZERO OUT THE FIRST 38 LOCS OF A VAC AREA

ZEROVAC		CAF	37DEC
ZVLOOP		TS	MPAC
		AD	FIXLOC
		TS	Q
		CAF	ZERO
		INDEX	Q
		TS	0
		CCS	MPAC
		TC	ZVLOOP
		TC	RE-ENTER

37DEC		DEC	37


# ROUTINE TO CONVERT IS COMP. NOS. TO 1S COMP.

CDULOGIC	CCS	MPAC		# THIS BASIC ROUTINE TESTS CDU ANGLES FOR
		TC	CDULOG1		# +OR-SIGN INCLUDING ZERO AND FORMS A DP
		TC	CDULOG1		# NUMBER CORRESPONDING TO ANGLE
		TC	+1
		CS	HALF		# USE		SMOVE	1
		TC	+2		# 		RTB
CDULOG1		CS	ZERO		# 			CDUXYZ
		XCH	OVCTR		#			CDULOGIC
		XCH	MPAC
		EXTEND
		MP	HALF
		XCH	OVCTR
		AD	LP
		XCH	MPAC +1
		XCH	OVCTR
		XCH	MPAC
		TC	DANZIG


# ROUTINE TO CONVERT 1S COMP. NOS. TO 2S COMP.

1STO2S		CAF	ZERO
		XCH	MPAC +1
		DOUBLE
		TS	OVCTR
		CAF	ZERO
		AD	MPAC
		AD	MPAC
		CCS	A
		AD	ONE
		TC	+2
		COM
ZYXR		TS	MPAC		# AND MAYBE OVERFLOW.
		TC	DANZIG
		
		INDEX	A		# HANDLE OVERFLOW IN STANDARD ANGULAR WAY.
		CAF	LIMITS
		AD	MPAC		# GUARANTEED NO OVERFLOW.
		TC	ZYXR


READPIPS	INHINT
		CS	PIPAX
		CS	A
		INDEX	FIXLOC
		TS	VAC
		CS	PIPAY
		CS	A
		INDEX	FIXLOC
		TS	VAC +2
		CS	PIPAZ
		CS	A
		INDEX	FIXLOC
		TS	VAC +4
		RELINT
		CAF	ZERO
		INDEX	FIXLOC
		TS	VAC +1
		INDEX	FIXLOC
		TS	VAC +3
		INDEX	FIXLOC
		TS	VAC +5
VMODE		CS	ZERO
		TC	DPEXIT +1



PULSEIMU	TC	BANKCALL
		CADR	IMUPULSE
		
		TC	RE-ENTER


SGNAGREE	TC	BANKCALL
		CADR	TPAGREE
		TC	DANZIG



# ROUTINE TO COMPLETE OPTICS TRUNNION ANGLE CONVERSION FROM COUNTER
# READING TO DP REVOLUTIONS. CALLS TO TRUNLOG SHOULD BE IMMEDIATELY
# PRECEDED BY A CALL TO CDULOGIC.  (NO NEED TO CHECK SXT POWER-ON BIT.)
TRUNLOG		CAF	BIT13
		MASK	WASOPSET
		CCS	A
		TC	+4

		CAF	HALF
TRUNLOG1	TC	SHORTMP
		TC	DANZIG		# WITH PD IF AT END W/ NO ADDRESSES.

		CAF	10DEGS		# CORRECT FOR 20 DEG OFFSET (CDULOGIC
		AD	MPAC		#  ALREADY SHIFTED IT RIGHT ONE) AND SHIFT
		TS	MPAC		#  RIGHT TWO ADDITIONAL PLACES.
		CAF	QUARTER
		TC	TRUNLOG1
		
10DEGS		DEC	3600		# HALF OF SXT TRUNION OFFSET
